C MHP 3/09  SUBROUTINE FOR READING IN ALEXANDER LOW T OPACITIES.
C
C ALEXANDER OPACITIES FERGUSON ET AL. 2005  APJ,623,585
C FOR EACH  X = 0.0, 0.1, 0.2, 0.35, 0.5, 0.7, 0.8, 0.9, 1-Z
C INCREASING ORDER OF Z = 0.0, 0.00001, 0.00003, 0.0001, 0.0003,
C     (16)               0.001, 0.002, 0.004, 0.01, 0.02, 0.03,
C                        0.04, 0.05, 0.06, 0.08, 0.10
C DESCENDING ORDER OF LOGT 4.5-3.5 IN 0.05 DEX INCREMENTS
C     (85)                 3.5-2.9 IN 0.01 DEX INCREMENTS
C                          2.9-2.7 IN 0.05 DEX INCREMENTS
C ASCENDING ORDER OF  LOGR -8.0 -7.5 -7.0 -6.5 -6.0 -5.5 -5.0 -4.5 -4.0 -3.5
C     (19)                -3.0 -2.5 -2.0 -1.5 -1.0 -0.5  0.0  0.5  1.0
C WHERE R = RHO/T_6**3
C READ IN ALL Z VALUES AT FIXED X.  THEN GENERATE SURFACE X,Z TABLE.
C INTERPOLATION BETWEEN COMPOSITIONS IS 4-POINT LAGRANGIAN.
C
C MHP 8/25 Added file name to subroutine call
      SUBROUTINE READALEX06(FALEX06)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      PARAMETER (NUMX=9)
      PARAMETER (NUMZ=16)
      PARAMETER (NUMXZ=143)
      PARAMETER (NUMT=85)
      PARAMETER (NUMD=19)
      CHARACTER*256 FALEX06
C     ALEX LOW T OPACITY COMMON BLOCKS
C MHP 8/25 Removed file names from common block
      COMMON /ALEX06/IALEX06
      COMMON /GALOT06/TGR06(NUMT),XXG06(NUMX),R0GR06(NUMD),ZZG06(NUMZ)
      COMMON /ALOT06/CAPALEX06(NUMT,NUMD),XE,ZE,IX,IT,ID
      COMMON /ALOT06ALL/CAPTALEX06(NUMXZ,NUMT,NUMD)
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
C     LOCAL ARRAYS
      REAL*8 R0GRTAB(NUMD),XX,ZZ,T
      DATA XXG06/0.0D0,0.1D0,0.2D0,0.35D0,0.5D0,0.7D0,0.8D0,0.9D0,
     *         1.0D0/
      DATA ZZG06/0.0D0, 0.00001D0, 0.00003D0, 0.0001D0, 0.0003D0,
     +         0.001D0, 0.002D0, 0.004D0, 0.01D0, 0.02D0, 0.03D0,
     +         0.04D0, 0.05D0,0.06D0, 0.08D0, 0.10D0/
      DATA TGR06/2.70D0,2.75D0,2.80D0,2.85D0,2.90D0,2.91D0,2.92D0,
     *    2.93D0,2.94D0,2.95D0,2.96D0,2.97D0,2.98D0,2.99D0,3.00D0,
     *    3.01D0,3.02D0,3.03D0,3.04D0,3.05D0,3.06D0,3.07D0,3.08D0,
     *    3.09D0,3.10D0,3.11D0,3.12D0,3.13D0,3.14D0,3.15D0,3.16D0,
     *    3.17D0,3.18D0,3.19D0,3.20D0,3.21D0,3.22D0,3.23D0,3.24D0,
     *    3.25D0,3.26D0,3.27D0,3.28D0,3.29D0,3.30D0,3.31D0,3.32D0,
     *    3.33D0,3.34D0,3.35D0,3.36D0,3.37D0,3.38D0,3.39D0,3.40D0,
     *    3.41D0,3.42D0,3.43D0,3.44D0,3.45D0,3.46D0,3.47D0,3.48D0,
     *    3.49D0,3.50D0,3.55D0,3.60D0,3.65D0,3.70D0,3.75D0,3.80D0,
     *    3.85D0,3.90D0,3.95D0,4.00D0,4.05D0,4.10D0,4.15D0,4.20D0,
     *    4.25D0,4.30D0,4.35D0,4.40D0,4.45D0,4.50D0/
      DATA R0GR06/-8.0D0,-7.5D0,-7.0D0,-6.5D0,-6.0D0,-5.5D0,-5.0D0,
     *   -4.5D0,-4.0D0,-3.5D0,-3.0D0,-2.5D0,-2.0D0,-1.5D0,-1.0D0,
     *   -0.5D0, 0.0D0, 0.5D0, 1.0D0/
C INITIALIZE INDEX OF PREVIOUS CLOSEST POINTS AND ENVELOPE ABUNDANCES
      DATA XE,ZE,IX,IT,ID/0.0D0,0.0D0,4,43,10/
      SAVE
      OPEN(UNIT=IALEX06,FILE=FALEX06)
C READ IN INITIAL X AND Z; ENSURE THAT THEY HAVE THE EXPECTED VALUES.
      DO I = 1,NUMX-1
         DO II = 1, NUMZ
C INDEX FOR STORING TABLES : 15 SETS OF METAL ABUNDANCE WITH 7 SETS
C OF HYDROGEN FOR EACH.  THESE ARE STORED IN THE ORDER (X,Z) OF
C (0,0),(0,0.1),...(0.9,0),(0.9,0.1).  The final tables are defined
C at X = 1-Z except for the 0.1 Z case (already read in at X=0.9).
C HEADER INFORMATION: X AND Z; CHECK FOR CONSISTENCY
            READ(IALEX06,10)XX,ZZ
 10   FORMAT(30X,F9.6,7X,F9.6//)
            IF(XX.NE.XXG06(I) .OR. ZZ.NE.ZZG06(II))THEN
               WRITE(*,15)XXG06(I),XX,ZZG06(II),ZZ
 15   FORMAT(1X,'ERROR IN ALEXANDER OPACITY TABLES:'/
     *       1X,'EXPECTED AND ACTUAL X,Z',4F7.2,' RUN STOPPED')
C               STOP
            ENDIF
            READ(IALEX06,20)(R0GRTAB(K),K=1,NUMD)
 20   FORMAT(6X,19F7.3)
            DO KK=1,16
               IF(R0GRTAB(KK).NE.R0GR06(KK))THEN
                  WRITE(*,25)KK,R0GR06(KK),R0GRTAB(KK)
 25    FORMAT(1X,'ERROR IN ALEXANDER OPACITY TABLES:'/
     *        1X,'EXPECTED AND ACTUAL R',I3,2F7.3,' RUN STOPPED')
                  STOP
               ENDIF
            END DO
            JJ = (I-1)*NUMZ+II
C OPACITY INFORMATION AT EACH SHELL: CHECK FOR CONSISTENCY WITH T.
C STORE IN A NUMXZ*NUMT*NUMR ARRAY.
            DO J = NUMT,1,-1
               READ(IALEX06,30) T,(CAPTALEX06(JJ,J,K),K=1,NUMD)
 30    FORMAT(F5.3,1X,19F7.3)
               IF(T.NE.TGR06(J))THEN
                  WRITE(*,35)J,TGR06(J),T
 35    FORMAT(1X,'ERROR IN ALEXANDER OPACITY TABLES:'/
     *        1X,'EXPECTED AND ACTUAL T',I3,2F7.3,' RUN STOPPED')
                  STOP
               ENDIF
            END DO
         END DO
      END DO
C FINAL SET OF X TABLES ARE DEFINED AT X = 1 - Z AND THERE IS NOT ONE FOR
C Z = 0.1 (ALREADY READ IN AT X=0.9).
      DO II = 1, NUMZ-1
C INDEX FOR STORING TABLES : 15 SETS OF METAL ABUNDANCE WITH 7 SETS
C OF HYDROGEN FOR EACH.  THESE ARE STORED IN THE ORDER (X,Z) OF
C (0,0),(0,0.1),...(0.9,0),(0.9,0.1).  The final tables are defined
C at X = 1-Z except for the 0.1 Z case (already read in at X=0.9).
C HEADER INFORMATION: X AND Z; CHECK FOR CONSISTENCY
         READ(IALEX06,10)XX,ZZ
         XTEST = 1.0D0 - ZZG06(II)
         IF(XX.NE.XTEST .OR. ZZ.NE.ZZG06(II))THEN
            WRITE(*,15)XTEST,XX,ZZG06(II),ZZ
            STOP
         ENDIF
         READ(IALEX06,20)(R0GRTAB(K),K=1,NUMD)
         DO KK=1,16
            IF(R0GRTAB(KK).NE.R0GR06(KK))THEN
               WRITE(*,25)KK,R0GR06(KK),R0GRTAB(KK)
               STOP
            ENDIF
         END DO
         JJ = (NUMX-1)*NUMZ+II
C OPACITY INFORMATION AT EACH SHELL: CHECK FOR CONSISTENCY WITH T.
C STORE IN A NUMXZ*NUMT*NUMR ARRAY.
         DO J = NUMT,1,-1
            READ(IALEX06,30) T,(CAPTALEX06(JJ,J,K),K=1,NUMD)
            IF(T.NE.TGR06(J))THEN
               WRITE(*,35)J,TGR06(J),T
               STOP
            ENDIF
         END DO
      END DO
      CLOSE(UNIT=IALEX06)
C INITIALIZE FIXED Z,X TABLE
      DO I = 1,NUMT
         DO J = 1,NUMD
            CAPALEX06(I,J)=CAPTALEX06(1,I,J)
         END DO
      END DO
      RETURN
      END
