C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     OVERSH
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE OVERSH(HCOMP,HD,HP,HR,HS,HT,M,MXZONE,MXZON0,NZONE)

C  THIS SR COMPUTES THE LOCAL PRESSURE SCALE HEIGHT AT BOTH EDGES OF A
C  GIVEN CONVECTIVE REGION AND LOCATES THE BOUNDARIES OF OVERSHOOT
C  REGIONS BASED ON THE USER-SPECIFIED EXTENT.

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)

      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/DPMIX/DPENV,ALPHAC,ALPHAE,ALPHAM,BETAC,IOV1,IOV2,
     *      IOVIM, LOVSTC, LOVSTE, LOVSTM, LSEMIC, LADOV, LOVMAX
      COMMON/ROT/WNEW,WALPCZ,ACFPFT,ITFP1,ITFP2,LROT,LINSTB,LWNEW
      DIMENSION HCOMP(15,JSON),HD(JSON),HP(JSON),HR(JSON),HS(JSON),
     *          HT(JSON),MXZONE(12,2),MXZON0(12,2)
      SAVE

      IOV1 = -1
      IOV2 = -1
      DO 100 I = 1,NZONE
C DETERMINE IF THIS REGION IS A CORE CONVECTION ZONE, SURFACE CZ,
C OR INTERMEDIATE CZ. THERE ARE SEPARATE FLAGS GOVERNING WHETHER
C OVERSHOOT WILL BE PERFORMED IN EACH CASE, AND SEPARATE USER
C PARAMETERS GOVERNING THE DEGREE OF OVERSHOOT.
         IF(MXZONE(I,1).EQ.1)THEN
C CONVECTIVE CORE
C CHECK FOR A FULLY CONVECTIVE STAR; SKIP THIS SR IF THERE IS ONE.
            IF(MXZONE(I,2).EQ.M)THEN
               WRITE(ISHORT,5)
    5          FORMAT(1X,'FULLY CONVECTIVE MODEL - NO OVERSHOOT')
               WRITE(ISHORT,200)(MXZON0(1,J),J= 1,2)
               RETURN
            ENDIF
C SKIP IF NO CORE OVERSHOOT IS DESIRED.
            IF(.NOT.LOVSTC)GOTO 100
            LFLAGU = .TRUE.
            LFLAGD = .FALSE.
            JEDGE = MXZONE(I,2)
            CALL HSUBP (HCOMP,HD,HP,HR,HS,HT,JEDGE,PSCALU)
C PSCALU IS THE PRESSURE SCALE HEIGHT ABOVE THE CONVECTIVE REGION;
C ALPHAC IS THE DESIRED OVERSHOOT (IN SCALE HEIGHTS).
C            PSCALU = PSCALU*ALPHAC
C JVS 07/13 ALLOW FOR THE LIMITING OF OVERSHOOTING ABOVE THE CONVECTIVE
C CORES OF LOVE MASS STARS AS PER WOO & DEMARQUE 2001
            IF (LOVMAX) THEN
                  PSCALU = MIN(PSCALU*ALPHAC, BETAC*EXP(CLN*HR(JEDGE)))
            ELSE
                  PSCALU = PSCALU*ALPHAC
            ENDIF

         ELSE IF(MXZONE(I,2).EQ.M)THEN
C CONVECTIVE ENVELOPE
C SKIP IF NO ENVELOPE OVERSHOOT IS DESIRED.
            IF(.NOT.LOVSTE)GOTO 100
            LFLAGU = .FALSE.
            LFLAGD = .TRUE.
            JEDGE = MXZONE(I,1)
            CALL HSUBP (HCOMP,HD,HP,HR,HS,HT,JEDGE,PSCALD)
C PSCALD IS THE PRESSURE SCALE HEIGHT BELOW THE CONVECTIVE REGION;
C ALPHAE IS THE DESIRED OVERSHOOT (IN SCALE HEIGHTS).
            PSCALD = PSCALD*ALPHAE
         ELSE
C INTERMEDIATE CONVECTION ZONE (NOT INCLUDING CENTRAL OR SURFACE POINT).
C SKIP IF NO INTERMEDIATE CONVECTION.
            IF(.NOT.LOVSTM)GOTO 100
            LFLAGU = .TRUE.
            LFLAGD = .TRUE.
C PSCALU AND PSCALD HAVE THE SAME MEANING AS ABOVE; OVERSHOOT BOTH BELOW
C AND ABOVE IS PERFORMED BY AN AMOUNT ALPHAM.
            JEDGE = MXZONE(I,1)
            CALL HSUBP (HCOMP,HD,HP,HR,HS,HT,JEDGE,PSCALD)
            PSCALD = PSCALD*ALPHAM
            JEDGE = MXZONE(I,2)
            CALL HSUBP (HCOMP,HD,HP,HR,HS,HT,JEDGE,PSCALU)
            PSCALU = PSCALU*ALPHAM
         ENDIF
C COMPUTE EXTENSION OF CONVECTION ZONE BELOW SCHWARTZSCHILD BOUNDARY.
         IF(LFLAGD)THEN
            ID = MXZONE(I,1)
            RCZ = EXP(CLN*HR(ID))
            ROS = RCZ - PSCALD
C THE OVERSHOOT REGION IS EXTENDED THE RADIAL DISTANCE PSCALD DOWN; THE
C LAST POINT LESS THAN PSCALD FROM THE FORMAL EDGE OF THE CZ IS DEFINED
C AS THE NEW EDGE OF THE MIXED REGION.
            DO 10 J = ID-1,1,-1
               R = EXP(CLN*HR(J))
               IF(R.LT.ROS)GOTO 20
   10       CONTINUE
C IF THE CODE GETS HERE, THE OVERSHOOT REGION EXTENDS BELOW THE FIRST POINT.
C THE CODE WILL ASSIGN THE FIRST POINT AS THE LOWER EDGE(I.E. THE CZ WILL
C EXTEND TO THE CENTER).
            J = 0
   20       CONTINUE
C FOR ROTATING MODELS, ENSURE THAT THERE IS AT LEAST ONE RADIATIVE POINT
C IN THE OVERSHOOT REGION.
            MXZONE(I,1) = J + 1
C 11/91 MHP CHANGED TO REQUIRE AN OVERSHOOT ZONE ONLY IF LINSTB=T.
            IF(LROT .AND. LINSTB .AND. MXZONE(I,1).EQ.MXZON0(I,1))
     *      MXZONE(I,1) = MXZONE(I,1)-1
C DBG 8/94 STORE POSITION OF OVERSHOOT FOR ADIABATIC EXTENSION
            IOV2 = ID
            IOV1 = MXZONE(I,1)
         ENDIF
C COMPUTE EXTENSION OF CONVECTION ZONE ABOVE SCHWARTZSCHILD BOUNDARY.
         IF(LFLAGU)THEN
            ID = MXZONE(I,2)
            RCZ = EXP(CLN*HR(ID))
            ROS = RCZ + PSCALU
C THE OVERSHOOT REGION IS EXTENDED THE RADIAL DISTANCE PSCALU UP; THE
C LAST POINT LESS THAN PSCALU FROM THE FORMAL EDGE OF THE CZ IS DEFINED
C AS THE NEW EDGE OF THE MIXED REGION.
            DO 30 J = ID+1,M
               R = EXP(CLN*HR(J))
               IF(R.GT.ROS)GOTO 40
   30       CONTINUE
C IF THE CODE GETS HERE, THE OVERSHOOT REGION EXTENDS ABOVE THE LAST POINT.
C THE CODE WILL ASSIGN THE LAST POINT AS THE UPPER EDGE(I.E. THE CZ WILL
C EXTEND TO THE SURFACE).
            J = M + 1
   40       CONTINUE
            MXZONE(I,2) = J - 1
C 11/91 MHP CHANGED TO REQUIRE AN OVERSHOOT ZONE ONLY IF LINSTB=T.
            IF(LROT .AND. LINSTB .AND. MXZONE(I,2).EQ.MXZON0(I,2))
     *      MXZONE(I,2) = MXZONE(I,2)+1
         ENDIF
  100 CONTINUE
C OUTPUT : THE OLD AND NEW MIXED REGIONS ARE PRINTED OUT IN ISHORT.
      WRITE(ISHORT,200)((MXZON0(I,J),J= 1,2),I=1,NZONE)
  200 FORMAT(1X,'MIXED REGIONS WITHOUT OVERSHOOT',
     *       4('[',I4,'-',I4,' ]'))
      WRITE(ISHORT,210)((MXZONE(I,J),J= 1,2),I=1,NZONE)
  210 FORMAT(1X,'MIXED REGIONS WITH OVERSHOOT   ',
     *       4('[',I4,'-',I4,' ]'))

      RETURN
      END
