C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     GETOPAC
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE GETOPAC(DL,TL,X,Z,O,OL,QOD,QOT,FXION)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      REAL*8 OLAOL,OXA,OT,ORHO,TOLLAOL
      REAL*8 FXION(3)
C MHP 8/25 Removed unused variables
C      CHARACTER*256 FLAOL, FPUREZ, FKUR2, FcondOpacP
C OPACITY COMMON BLOCKS - modified 3/09
      COMMON /NEWOPAC/ZLAOL1,ZLAOL2,ZOPAL1,ZOPAL2, ZOPAL951,
     +       ZALEX1, ZKUR1, ZKUR2,TMOLMIN,TMOLMAX,LALEX06,
     +       LLAOL89,LOPAL92,LOPAL95,LKUR90,LALEX95,L2Z
      COMMON/COMP/XENV,ZENV,ZENVM,AMUENV,FXENV(12),XNEW,ZNEW,STOTAL,
     *     SENV
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
C MHP 8/25 Removed character file names from common block
      COMMON/NWLAOL/OLAOL(12,104,52),OXA(12),OT(52),ORHO(104),TOLLAOL,
     *     IOLAOL, NUMOFXYZ, NUMRHO, NUMT, LLAOL, LPUREZ, IOPUREZ
      COMMON/OPTAB/OPTOL,ZSI,IDT,IDD(4)
C MHP 8/25 Removed character file names from common block
      COMMON /MISCOPAC/IKUR2,IcondOpacP,LcondOpacP
      SAVE
C
C     THIS SUBROUTINE CALCULATES THE OPACITY FOR A GIVEN X AND Z.
C     IF LDIFZ=T OR LZRAMP=T THEN INTERPOLATE BETWEEN TWO Z TABLES.
C     IN A SMALL T RANGE THE ATMOSPHERE AND INTERIOR OPACITY ARE
C     RAMPED FROM ONE TO THE OTHER.
C
C
C     GET ATMOSPHERE OPACITY
C
      LGOTATM = .FALSE.
      IF(TL.LE.TMOLMAX)THEN
         IF (LALEX06) THEN
            CALL GETALEX06(DL,TL,X,Z, SO, SOL,SQOD,SQOT)
C             RR = DL - 3.0D0*(TL-6.0D0)
C             WRITE(*,911)X,Z,RR,TL,SOL
C 911         FORMAT(2f5.2,3f7.3)
          LGOTATM = .TRUE.
         ELSE IF (LALEX95) THEN
            CALL YALO3D(DL, TL, X, Z, SO, SOL, SQOD, SQOT)
          LGOTATM = .TRUE.
         ELSE IF (LKUR90) THEN
            CALL KURUCZ(DL, TL, SO, SOL, SQOD, SQOT, *100)
          IF (L2Z) THEN
             CALL KURUCZ2(DL, TL, SO1, SOL1, SQOD1, SQOT1, *100)
             SLOPE = (SOL-SOL1)/(ZKUR1-ZKUR2)
               SOL = SOL1 + (Z-ZKUR2)*SLOPE
             SO = 10.0D0**SOL
             SLOPE = (SQOD-SQOD1)/(ZKUR1-ZKUR2)
             SQOD = SQOD1 + (Z-ZKUR2)*SLOPE
             SLOPE = (SQOT-SQOT1)/(ZKUR1-ZKUR2)
             SQOT = SQOT1 + (Z-ZKUR2)*SLOPE
           END IF
           LGOTATM = .TRUE.
         END IF
      ENDIF
  100 CONTINUE

C     GET INTERIOR OPACITY IF NEEDED

      IF (TL .LT. TMOLMIN.AND.LGOTATM) GOTO 1000

C     HELIUM BURNING REGION (HB EVOLUTION) USE PURE Z TABLE
C mhp 7/12 Altered logic of the opacities in the He burnng
C regime.  Switched to exclusive usage of OPAL below 50 million K
C and switched the ramp to above Z = 0.1.
      IF((Z .GT. 0.1D0) .AND. (TL.GT.7.7D0)) THEN
C      IF((Z .GT. 0.15D0) .OR.
C     *     ((ABS(Z-ZENV) .GT. OPTOL).AND..NOT.L2Z)) THEN
         IF(.NOT.LPUREZ) THEN
C            WRITE(ISHORT, *)' ERROR: Z.NE.ZENV. NEED PURE Z',
C     *        ' TABLE TO CONTINUE. Z,ZENV=',Z, ZENV
            WRITE(ISHORT, *)' ERROR: Z>0.10 T > 5 X 10^7 K',
     *        ' NEED PURE Z TABLE TO CONTINUE. Z,LOG T=',Z, TL
              STOP
       END IF
       CALL GTPURZ(DL,TL,OZ,OLZ,QODZ,QOTZ)
         IF (LOPAL95) THEN
C MHP 7/12 INTERPOLATE TO MAXIMUM Z IN TABLE
             ZIT = 0.1D0
             CALL GETOPAL95(DL,TL,X,ZIT,O,OL,QOD,QOT)
C           ZIT=ZOPAL951
         ELSE IF (LOPAL92) THEN
           CALL YLLO3D(DL,TL,X,O,OL,QOD,QOT)
           ZIT=ZOPAL1
         ELSE IF (LLAOL89) THEN
           CALL GTLAOL(DL,TL,X,O,OL,QOD,QOT)
           ZIT=ZLAOL1
         END IF
       SLOPE = (OL-OLZ)/(ZIT-1.0D0)
       OL = OLZ + (Z-1.0D0)*SLOPE
       O = 10.0D0**OL
       SLOPE = (QOD-QODZ)/(ZIT-1.0D0)
       QOD = QODZ + (Z-1.0D0)*SLOPE
       SLOPE = (QOT-QOTZ)/(ZIT-1.0D0)
       QOT = QOTZ + (Z-1.0D0)*SLOPE
C      ELSE
      ELSE IF((Z.GT.0.12D0) .OR. ((ABS(Z-ZENV) .GT. OPTOL)
     *   .AND..NOT.L2Z .AND. .NOT.LOPAL95))THEN
         WRITE(ISHORT,*)' Z>0.12 T < 5 X 10^7 K',
     *   ' OUTSIDE OPAL OPACITY TABLE RANGE OR Z',
     *   ' OUTSIDE SINGLE TABLE USED.Z,ZENV,LOG T=',Z,ZENV,TL
         STOP
C
C     NOT HELIUM BURNING REGION (HB EVOLUTION) OR L2Z=T AND
C     Z STILL NOT TOO LARGE IN CORE (<.15) SO CAN USE
C     SECOND Z TABLE RATHER THAN PURE Z TABLE
C
C      IF (LOPAL95) THEN
      ELSE IF (LOPAL95) THEN
         CALL GETOPAL95(DL,TL,X,Z,O,OL,QOD,QOT)
      ELSE IF (LOPAL92) THEN
         CALL YLLO3D(DL,TL,X,O,OL,QOD,QOT)
         IF (L2Z) THEN
            CALL YLLO3D2(DL,TL,X,O1,OL1,QOD1,QOT1)
          SLOPE = (OL-OL1)/(ZOPAL1-ZOPAL2)
          OL = OL1 + (Z-ZOPAL2)*SLOPE
          O = 10.0D0**OL
          SLOPE = (QOD-QOD1)/(ZOPAL1-ZOPAL2)
          QOD = QOD1 + (Z-ZOPAL2)*SLOPE
          SLOPE = (QOT-QOT1)/(ZOPAL1-ZOPAL2)
          QOT = QOT1 + (Z-ZOPAL2)*SLOPE
       END IF
      ELSE IF (LLAOL89) THEN
         CALL GTLAOL(DL,TL,X,O,OL,QOD,QOT)
         IF (L2Z) THEN
          CALL GTLAOL2(DL,TL,X,O1,OL1,QOD1,QOT1)
          SLOPE = (OL-OL1)/(ZLAOL1-ZLAOL2)
          OL = OL1 + (Z-ZLAOL2)*SLOPE
          O = 10.0D0**OL
          SLOPE = (QOD-QOD1)/(ZLAOL1-ZLAOL2)
          QOD = QOD1 + (Z-ZLAOL2)*SLOPE
          SLOPE = (QOT-QOT1)/(ZLAOL1-ZLAOL2)
          QOT = QOT1 + (Z-ZLAOL2)*SLOPE
       END IF
C MHP 7/12 INSERT FINAL TRAP - NO OPACITY COMPUTED
C SHOULD NOT BE ABLE TO GET HERE.
      ELSE
         WRITE(ISHORT,*)'NO OPACITY TABLE CHOSEN',
     *   ' RUN STOPPED. X Z TL=',X,Z,TL
         STOP
      END IF
C      END IF
C
 1000 CONTINUE
C     DO A RAMP BETWEEN SURFACE AND INTERIOR OPACITY

      IF (LGOTATM .AND. TL .LE. TMOLMAX) THEN
         IF( TL.GE.TMOLMIN) THEN
C             RR = DL - 3.0D0*(TL-6.0D0)
C             WRITE(*,*)RR,TL,OL,SOL
            RMPWT = (TL-TMOLMIN)/(TMOLMAX-TMOLMIN)
            O = RMPWT*O + (1.0D0-RMPWT)*SO
            OL = DLOG10(O)
            QOD = RMPWT*QOD + (1.0D0-RMPWT)*SQOD
            QOT = RMPWT*QOT + (1.0D0-RMPWT)*SQOT
       ELSE
            O = SO
            OL = SOL
            QOD = SQOD
            QOT = SQOT
       END IF
      END IF
C     DO CONDUCTIVE OPACITY CORRECTION
      IF (LcondOpacP) THEN
C Get Potekhin conductive opacity
            CALL condOpacPInt(DL,TL,X,Z,OC,OCL,QODC,QOTC,FXION,LCONDO)
      ELSE
         LCONDO = .FALSE.
      END IF

      IF (.NOT. LCONDO) THEN
C If we get here we have no Potekhin opacity, so we
C try for Hubbard Lampe
         IF(TL.LT.4.2D0) THEN
            RETURN
         ELSE IF(DL.LT.(2.0D0*TL-13.0D0)) THEN
            RETURN
         ELSE
C Do Hubbard Lampe conductive opacity calculation
          COND = DLOG10(1.0D0-0.6D0*X) - 14.6196D0 -
     *             (3.5853D0+0.1386D0*DL)*DL +
     *             (5.1324D0-0.3219D0*TL)*TL + 0.3901D0*DL*TL
          OC = 10.0D0**COND
            QODC = 0.3901D0*TL - 0.2772D0*DL - 3.5853D0
            QOTC = 0.3901D0*DL - 0.6438D0*TL + 5.1324D0
         ENDIF
      ENDIF

      OX = O   ! Save the radiative opacity stuff
      QODX = QOD
      QOTX = QOT
C Add the opacities apppropriately
      O = OX*OC/(OX + OC)  ! e.g. 1/O = 1/OX + 1/OC
      OL = DLOG10(O)
      QOD = (QODX + QODC - (OX*QODX + OC*QODC)/(OX + OC))
      QOT = (QOTX + QOTC - (OX*QOTX + OC*QOTC)/(OX + OC))

      RETURN
      END
