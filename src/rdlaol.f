C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     READ LAOL89
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE RDLAOL(V,FLAOL,FLAOL2)
C DBG 4/94 MOdified to read in second table for ZRAMP core stuff
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      REAL*8 OLAOL(12,104,52),OXA(12),OT(52),ORHO(104),TOLLAOL
      REAL*8 OLAOL2(12,104,52),OXA2(12),OT2(52),ORHO2(104)
      CHARACTER*256 FLAOL,FLAOL2
C MHP 8/25 Removed character file names from common block
      COMMON/NWLAOL/OLAOL, OXA, OT, ORHO, TOLLAOL,
     *  IOLAOL, NUMOFXYZ, NUMRHO, NUMT, LLAOL, LPUREZ, IOPUREZ
C DBG 4/94 New common block for second opacity table
      COMMON/NWLAOL2/OLAOL2, OXA2, OT2, ORHO2, NXYZ2,NRHO2,NT2
C MHP 8/25 Removed character file names from common block
      COMMON/ZRAMP/RSCLZC(50), RSCLZM1(50), RSCLZM2(50),
     *             IOLAOL2, IOOPAL2, NK,
     *             LZRAMP
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
C DBG 7/92 COMMON BLOCK ADDED TO COMPUTE DEBYE-HUCKEL CORRECTION.
      COMMON/DEBHU/CDH,ETADH0,ETADH1,ZDH(18),XXDH,
     1             YYDH,ZZDH,DHNUE(18),LDH
C OPACITY COMMON BLOCKS - modified 3/09
      COMMON /NEWOPAC/ZLAOL1,ZLAOL2,ZOPAL1,ZOPAL2, ZOPAL951,
     +       ZALEX1, ZKUR1, ZKUR2,TMOLMIN,TMOLMAX,LALEX06,
     +       LLAOL89,LOPAL92,LOPAL95,LKUR90,LALEX95,L2Z
      DIMENSION V(12), V2(12), ZDH2(18)
      SAVE
      OPEN(UNIT=IOLAOL,FILE=FLAOL, FORM='FORMATTED',
     *     STATUS='OLD')
C     READ IN ARRAY SIZES
      READ(IOLAOL,100)NUMOFXYZ,NUMRHO,NUMT
  100 FORMAT(/,18X,I2,9X,I3,14X,I3)
      IF(NUMOFXYZ.GT.11.OR.NUMRHO.GT.104.OR.NUMT.GT.52) THEN
         WRITE(ISHORT,*)' OPACITY ARRAY TOO LARGE.'
         STOP
      END IF
C     READ IN RELATIVE ABUNDANCES BY WEIGTH OF THE METALS C,N,O,NE,NA,
C     MG,SI,AR,FE (THE COX&STEWART MIX).  MIXTURE IS FOR THE ZLOT PART
C     OF THE OPACITY TABLE WITH LAOL RELATIVE ABUNDANCES SCALED
C     TO SUM TO ZLOT.
      READ(IOLAOL,120)ZLOT,V(6),V(9),V(8),V(11),V(1),V(3),V(2),V(5),
     *    V(10),V(4)
  120 FORMAT(47X,F8.5,//,1P6E12.5,/,1P4E12.5)
C     READ ZHIT THE METAL ABUNDANCE FOR THE HIT PART OF TABLE.
C DBG 7/92 NEED RELATIVE ABUNDANCES OF METALS FOR DEBYE-HUCKEL
C     CORRECTION. 18 ELEMENTS, C,N,O,Ne,Na,Mg,Al,Si,P,
C     S,Cl,Ar,Ca,Ti,Cr,Mn,Fe,Ni scaled to sum to ZHIT
      READ(IOLAOL,130)ZHIT,ZDH
C 130 FORMAT(54X,F8.5,///////)
  130 FORMAT(54X,F8.5,/////,1P6E12.5,/,1P6E12.5,/1P6E12.5)
C     READ IN H MASS FRACTIONS OF TABLE
      READ(IOLAOL,140)(OXA(II),II=1,NUMOFXYZ)
  140 FORMAT(/,(1P6E12.5))
C     READ IN DENSITY GRID OF TABLE
      READ(IOLAOL,150)(ORHO(II),II=1,NUMRHO)
  150 FORMAT(/,(1P6E12.5))
C     READ IN TEMPERATURE GRID OF TABLE
      READ(IOLAOL,160)(OT(II),II=1,NUMT)
  160 FORMAT(/,(1P6E12.5))
C     READ IN OPACITIES
      READ(IOLAOL,170)
  170 FORMAT(1X)
      DO IX=1,NUMOFXYZ
         DO IR=1,NUMRHO
            READ(IOLAOL,200)
  200       FORMAT(1X)
            READ(IOLAOL,210)(OLAOL(IX,IR,IT),IT=1,NUMT)
  210       FORMAT(1P6E12.5)
         END DO
      END DO
      CLOSE(IOLAOL)
C
C
C DBG 4/94 New stuff follows
      IF(L2Z) THEN
         OPEN(UNIT=IOLAOL2,FILE=FLAOL2, FORM='FORMATTED',
     *        STATUS='OLD')
C        READ IN ARRAY SIZES
         READ(IOLAOL2,100)NXYZ2,NRHO2,NT2
         IF(NXYZ2.GT.11.OR.NRHO2.GT.104.OR.NT2.GT.52) THEN
            WRITE(ISHORT,*)' SECOND OPACITY ARRAY TOO LARGE.'
            STOP
         END IF
C        READ IN RELATIVE ABUNDANCES BY WEIGTH OF THE METALS C,N,O,NE,NA,
C        MG,SI,AR,FE (THE COX&STEWART MIX).  MIXTURE IS FOR THE ZLOT PART
C        OF THE OPACITY TABLE WITH LAOL RELATIVE ABUNDANCES SCALED
C        TO SUM TO ZLOT2.
         READ(IOLAOL2,120)ZLOT2,V2(6),V2(9),V2(8),V2(11),
     *          V2(1),V2(3),V2(2),V2(5),V2(10),V2(4)
C        READ ZHIT2 THE METAL ABUNDANCE FOR THE HIT PART OF TABLE.
C        DBG 7/92 NEED RELATIVE ABUNDANCES OF METALS FOR DEBYE-HUCKEL
C        CORRECTION. 18 ELEMENTS, C,N,O,Ne,Na,Mg,Al,Si,P,
C        S,Cl,Ar,Ca,Ti,Cr,Mn,Fe,Ni scaled to sum to ZHIT
         READ(IOLAOL2,130)ZHIT2,ZDH2
C        READ IN H MASS FRACTIONS OF TABLE
         READ(IOLAOL2,140)(OXA2(II),II=1,NXYZ2)
C        READ IN DENSITY GRID OF TABLE
         READ(IOLAOL2,150)(ORHO2(II),II=1,NRHO2)
C        READ IN TEMPERATURE GRID OF TABLE
         READ(IOLAOL2,160)(OT2(II),II=1,NT2)
C        READ IN OPACITIES
         READ(IOLAOL2,170)
         DO IX=1,NXYZ2
            DO IR=1,NRHO2
               READ(IOLAOL2,200)
               READ(IOLAOL2,210)(OLAOL2(IX,IR,IT),IT=1,NT2)
            END DO
         END DO
         CLOSE(IOLAOL2)
      END IF
      RETURN
      END
