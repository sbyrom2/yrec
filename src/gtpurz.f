C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     GET LAOL OPACITY FOR PURE Z
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE GTPURZ(DL,TL,O,OL,QOD,QOT)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      REAL*8 DL,TL,O,OL,QOD,QOT
C MHP 8/25 Removed unused variables
C      CHARACTER*256 FLAOL, FPUREZ
      DIMENSION TOP(104),TR(104),TDOR(104), DODR(52)
      DIMENSION TOP1(52),TT(52), TDOT(52)
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
C MHP 8/25 Removed character file names from common block
      COMMON/NWLAOL/OLAOL(12,104,52), OXA(12), OT(52), ORHO(104),
     *  TOLLAOL,IOLAOL, NUMOFXYZ, NUMRHO, NUMT, LLAOL, LPUREZ,
     *  IOPUREZ
      COMMON/SLAOL/SLAOLL(12,104,52),SRHOL(12,104,52),
     *  SDORL(12,104,52), NUMRS(12,52)
C DBG 12/95 ARRAYS FOR PURE Z TABLE
      COMMON/ZLAOL/ZOLAOL(104,52), ZOT(52), ZORHO(104),
     *  NUMRHOZ, NUMTZ
      COMMON/ZSLAOL/ZSLAOLL(104,52),ZSRHOL(104,52),
     *  ZSDORL(104,52), NUMRSZ(52)
      SAVE
C
C     1. CUBIC SPLINE INTERPOLATE IN DENSITY
C     2. CUBIC SPLINE INTERPOLATE IN TEMPERATURE
C     IF WITHIN TOLLAOL OF EDGE THEN LINEAR EXTRAPOLATE
C
C     TOLLAOL PERMITS SOME EXTRAPLOATION BEYOND TABLE EDGE.
      TOLL = LOG(TOLLAOL)
      RTL = TL
      RDL = DL
      CALL LOCATE(ZOT,NUMTZ,RTL,JT)
      NUMTS = 0
C     GET RANGE OF FOUR TT SURROUNDING T
      CALL XRNG4(JT,NUMTZ,JT1,JT2)
      DO IT=JT1, JT2
         NUMS = NUMRSZ(IT)
         IF(NUMS .GE. 4) THEN
            DO IR=1, NUMS
               TOP(IR) = ZSLAOLL(IR,IT)
               TR(IR) = ZSRHOL(IR,IT)
               TDOR(IR) = ZSDORL(IR,IT)
            END DO
            IF (RDL.GT.TR(1) .AND. RDL.LT.TR(NUMS)) THEN
               CALL SPLINT(TR,TOP,NUMS,TDOR,RDL, ROL, KLO, KHI)
               NUMTS = NUMTS+1
               TOP1(NUMTS) = ROL
               TT(NUMTS) = ZOT(IT)
               DODR(NUMTS)=(TOP(KHI)-TOP(KLO))/(TR(KHI)-TR(KLO))
            ELSE IF (RDL.GT.TR(1)-TOLL .AND. RDL.LE.TR(1)) THEN
C              PUT LINEAR EXTRAPOLATION ROUTINES HERE
               SLOPE = (TOP(2)-TOP(1))/(TR(2)-TR(1))
               ROL = TOP(1)+SLOPE*(RDL-TR(1))
               NUMTS = NUMTS+1
               TOP1(NUMTS) = ROL
               TT(NUMTS) = ZOT(IT)
               DODR(NUMTS) = SLOPE
            ELSE IF (RDL.GE.TR(NUMS) .AND.
     *               RDL.LT.TR(NUMS)+TOLL) THEN
C              PUT LINEAR EXTRAPOLATION ROUTINES HERE
               SLOPE = (TOP(NUMS-1)-TOP(NUMS))/
     *                 (TR(NUMS-1)-TR(NUMS))
               ROL = TOP(NUMS)+SLOPE*(RDL-TR(NUMS))
               NUMTS = NUMTS+1
               TOP1(NUMTS) = ROL
               TT(NUMTS) = ZOT(IT)
               DODR(NUMTS) = SLOPE
            END IF
         ELSE
            WRITE(ISHORT,120)DL,TL
  120       FORMAT(' OUTSIDE Z OPACITY TABLE, IN DENSITY.  ',
     *     'LOG(RHO)=',1PE12.3, ' LOG(T)=', 1PE12.3)
            STOP
         END IF
      END DO
      IF(NUMTS .GE. 4) THEN
         CALL CSPLINE(TT,TOP1,NUMTS,1.0D30,1.0D30,TDOT)
         CALL SPLINT(TT,TOP1,NUMTS,TDOT,RTL, ROL, KLO, KHI)
         SLOPE=(DODR(KHI)-DODR(KLO))/(TT(KHI)-TT(KLO))
         QOD = DODR(KLO)+SLOPE*(RTL-TT(KLO))
         QOT = (TOP1(KHI)-TOP1(KLO))/(TT(KHI)-TT(KLO))
      ELSE
         WRITE(ISHORT,121)DL,TL
  121    FORMAT(' OUTSIDE Z OPACITY TABLE, IN TEMPERATURE.  ',
     *         'LOG(RHO)=',1PE12.3, ' LOG(T)=', 1PE12.3)
         STOP
      END IF
      IF(ROL .GT. 35) THEN
         O = 1.0D35
         OL = 35.0D0
      ELSE
         O = 10.0D0**ROL
         OL = ROL
      END IF
      RETURN
      END
