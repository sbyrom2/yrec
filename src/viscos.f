


C $$$$$$
C       SUBROUTINE VISCOS(HCOMP,HD,HT,LC,M)  ! KC 2025-05-31
      SUBROUTINE VISCOS(HCOMP,HD,HT,M)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)

      DIMENSION HCOMP(15,JSON),HD(JSON),HT(JSON),VISCX(11),WEIGHT(11),
C      *     Z(11),COMPN(11),VISCY(11),LC(JSON)  ! KC 2025-05-31
     *     Z(11),COMPN(11),VISCY(11)
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/SCRTCH/SESUM(JSON),SEG(7,JSON),SBETA(JSON),SETA(JSON),
     * LOCONS(JSON),SO(JSON),SDEL(3,JSON),SFXION(3,JSON),SVEL(JSON),SCP(JSON)
      COMMON/TEMP/CP(JSON),HAMU(JSON),SQDT(JSON),THDIF(JSON),
     *     VISC(JSON)
      REAL*8 LAMBD1
      DATA AMU/1.6605655D-24/
      DATA WEIGHT/1.007825D0,4.0026D0,1.0D0,3.01603D0,12.0D0,
     * 13.00335D0,14.00307D0,15.0011D0,15.99491D0,16.99491D0,
     * 17.99491D0/
      DATA Z/1.0D0,2.0D0,0.0D0,2.0D0,6.0D0,6.0D0,7.0D0,7.0D0,8.0D0,
     * 8.0D0,8.0D0/
      SAVE

C  SKIP CONVECTIVE ZONES
      DO 100 IM = 1,M
C  COMPUTE THE KINEMATIC MICROSCOPIC VISCOSITY DUE TO RADIATION AND IONS
C  CONVERT TO NUMBER DENSITIES AND FIND MEAN CHARGE PER ION(ZF) AND NE.
         O = SO(IM)
         ZF = 0.0D0
         COMSUM = 0.0D0
         DO 10 I = 1,11
            COMPN(I) = HCOMP(I,IM)/WEIGHT(I)
            COMSUM = COMSUM + COMPN(I)
            ZF = ZF + COMPN(I)*Z(I)
   10    CONTINUE
         ZF = ZF/COMSUM
         T = DEXP(CLN*HT(IM))
         T2 = T**2
         RHO = DEXP(CLN*HD(IM))
         ELECN = ZF*RHO/AMU
C  RADIATIVE DYNAMIC VISCOSITY (ELECTRON SCATTERING ONLY):
C  METHOD USED(VISCR) IS FROM LEDOUX,1958,HANDBUCH DER PHYSIK VOL.LI,P.445
C  ENDAL-SOFIA(VISCR2) METHOD REF. THOMAS,L.H. 1930,QUART.J.MATH,1,237
C  ENDAL-SOFIA VALUES CALCULATED FOR COMPARISON ONLY.
         VISCR = 6.7282653D-26*T2*T2/(O*RHO**2)
         VISCR2 = 3.36D-25*T2*T2/(HCOMP(1,IM)+1.0D0)/RHO**2
C  MOLECULAR DYNAMIC VISCOSITY
C  REF. SPITZER,1962,PHYSICS OF FULLY IONIZED GASES
C  AGAIN, ENDAL-SOFIA METHOD STORED IN VISMO2 FOR COMPARISON PURPOSES.
C  ACTUAL MOLECULAR VISCOSITY USED STORED IN VISMOL.
         FACTOR = DMIN1(1.0D0,4.2D5/T)
         LAMBD1 = 9.424536845D0 + 0.5D0*DLOG(T2*T*FACTOR/(ELECN*ZF**2))
         C = 3.125D-15*DSQRT(T)*T2
         VISMOL = 0.0D0
C  VISCX(I) IS THE MOLECULAR VISCOSITY OF SPECIES I.
         DO 30 I = 1,11
            IF(I.EQ.3) GOTO 30
            CON = C*COMPN(I)*DSQRT(WEIGHT(I))/
     *           ((LAMBD1 - DLOG(Z(I)))*Z(I)**2)
            SUM = 0.0D0
            DO 20 J = 1,11
               IF(J.EQ.3) GOTO 20
               SUM = SUM + COMPN(J)*Z(J)**2*
     *               DSQRT((WEIGHT(I) + WEIGHT(J))/WEIGHT(J))
   20       CONTINUE
            IF(DABS(SUM).LT.1.0D-38) GOTO 30
            VISCX(I) = CON/SUM
            IF(VISCX(I).GT.0.0D0) VISMOL = VISMOL + VISCX(I)
   30    CONTINUE
         VISMOL = VISMOL/RHO
         VISC(IM) = VISCR + VISMOL
C  NOW COMPUTE USING ENDAL-SOFIA METHOD
         VISMO2 = 0.0D0
         CON = 2.21D-15*DSQRT(T)*T2
         DO 40 I = 1,11
            IF(I.EQ.3) GOTO 40
            VISCY(I) = CON*HCOMP(I,IM)/(DSQRT(WEIGHT(I))*Z(I)**4*
     *                 (LAMBD1 - DLOG(Z(I))))
            IF(VISCY(I).GT.0.0D0) VISMO2 = VISMO2 + VISCY(I)
   40    CONTINUE
         VISMO2 = VISMO2/RHO/COMSUM
         VISC2 = VISCR2 + VISMO2
C  THERMAL DIFFUSIVITY(THDIF) DUE TO RADIATION IS CALCULATED
C  COMPONENT DUE TO THERMAL CONDUCTION OF MATTER IS NEGLECTED
C  RADIATIVE DIFFUSIVITY = K*T**3/(O*RHO**2*CP)
         THDIF(IM) = 1.6D1*CC13*5.669D-5*T*T2/(O*RHO**2*CP(IM))
  100 CONTINUE

      RETURN
      END
