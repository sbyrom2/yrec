C 1) USE KEMCOM TO IMPLICITLY SOLVE FOR ABUNDANCES IN RADIATIVE REGIONS.
C 2) SOLVE FOR BURNING IN EACH CONVECTION ZONE.

C       SUBROUTINE ROTMIX(DELTS,HCOMP,HS2,HT,ITLVL,M,MRZONE,MXZONE,  ! KC 2025-05-31
      SUBROUTINE ROTMIX(DELTS,HCOMP,HS2,HT,M,MRZONE,MXZONE,
     *                  NRZONE,NZONE
     *                  ,HSTOT,HD,HS,HR,HP,LC,HS1)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/BURN/HCOMPM(15,JSON)
      COMMON/CONST3/CDELRL,CMIXL,CMIXL2,CMIXL3,CLNDP,CSECYR
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
      COMMON/FLAG/LEXCOM
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/GRAVST/GRTOL,ILAMBDA,NITER_GS,LDIFY
      COMMON/GRAVS2/DT_GS,XMIN,YMIN,LTHOULFIT
c GES 6/15 INCLUDED COMMON BLOCK FOR NEW DIFFUSION ROUTINES.
      COMMON/GRAVS4/LNEWDIF,LDIFLI
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      COMMON/MDPHY/AMUM(JSON),CPM(JSON),DELM(JSON),DELAM(JSON),
     *             DELRM(JSON),ESUMM(JSON),OM(JSON),QDTM(JSON),
     *             THDIFM(JSON),VELM(JSON),VISCM(JSON),EPSM(JSON)
      COMMON/SCRTCH/SESUM(JSON),SEG(7,JSON),SBETA(JSON),SETA(JSON),
     * LOCONS(JSON),SO(JSON),SDEL(3,JSON),SFXION(3,JSON),SVEL(JSON),SCP(JSON)
      DIMENSION HS2(JSON),HT(JSON),HCOMP(15,JSON)
      DIMENSION HR1(JSON),HR2(JSON),HR3(JSON),HR4(JSON),
     * HR5(JSON),HR6(JSON),HR7(JSON),HR8(JSON),HR9(JSON),HR10(JSON),
     * HR11(JSON),HR12(JSON),HR13(JSON),HF1(JSON),HF2(JSON)
      DIMENSION MRZONE(13,2),MXZONE(12,2),
     * HD(JSON),HS(JSON),HR(JSON),HP(JSON),HS1(JSON),HQPR(JSON),
     * LC(JSON),TEMP(JSON),LCZ(JSON)
      SAVE
C NSPEC IS THE NUMBER OF SPECIES BEING TRACKED.
      IF(LEXCOM)THEN
         NSPEC = 15
      ELSE
         NSPEC = 11
      ENDIF
C  DDAGE IS THE TIMESTEP IN YEARS.
      DDAGE=DELTS/CSECYR
      DO II = 1,M
         HR1(II) = HCOMPM(1,II)
         HR2(II) = HCOMPM(2,II)
         HR3(II) = HCOMPM(3,II)
         HR4(II) = HCOMPM(4,II)
         HR5(II) = HCOMPM(5,II)
         HR6(II) = HCOMPM(6,II)
         HR7(II) = HCOMPM(7,II)
         HR8(II) = HCOMPM(8,II)
         HR9(II) = HCOMPM(9,II)
         HR10(II) = HCOMPM(10,II)
         HR11(II) = HCOMPM(11,II)
         HR12(II) = HCOMPM(12,II)
         HR13(II) = HCOMPM(13,II)
         HF1(II) = HCOMPM(14,II)
         HF2(II) = HCOMPM(15,II)
      END DO
C
C  NOW IMPLICITLY SOLVE FOR THE NEW ABUNDANCES AT THE END OF THE
C  TIMESTEP.  THIS IS DONE SHELL BY SHELL FOR RADIATIVE REGIONS,
C  AND FOR EACH CONVECTION ZONE AS A UNIT.
C
C RADIATIVE ZONES.
C
      DO 40 K = 1,NRZONE
         DO 30 J = MRZONE(K,1),MRZONE(K,2)
C EXIT LOOP ONCE T DROPS BELOW NUCLEAR REACTION T CUTOFF
            IF(HT(J).LE.TCUT(1)) GOTO 45
            IBEGIN = J
            IEND = J
            CALL KEMCOM(HT,IBEGIN,IEND,HR1,HR2,HR3,HR4,HR5,HR6,HR7,
C      *                   HR8,HR9,HR10,HR11,HR12,HR13,HF1,HS2,HCOMP,
C      *                   DDAGE,ITLVL)  ! KC 2025-05-31
     *                   HR8,HR10,HR11,HR12,HF1,HS2,HCOMP,DDAGE)
   30    CONTINUE
   40 CONTINUE
   45 CONTINUE
C
C CONVECTION ZONES.
C NOTE KEMCOM ALSO AUTOMATICALLY HOMOGENIZE CONVECTION ZONES.
C
      DO 50 K = 1,NZONE
         IBEGIN = MXZONE(K,1)
         IEND = MXZONE(K,2)
         CALL KEMCOM(HT,IBEGIN,IEND,HR1,HR2,HR3,HR4,HR5,HR6,HR7,
C      *                HR8,HR9,HR10,HR11,HR12,HR13,HF1,HS2,HCOMP,
C      *                DDAGE,ITLVL)  ! KC 2025-05-31
     *                HR8,HR10,HR11,HR12,HF1,HS2,HCOMP,DDAGE)
   50 CONTINUE
C
C MICROSCOPIC DIFFUSION OF HELIUM.
C
C***BC 5/92 ROTMIX MODIFIED TO INCLUDE CALL TO GRAVITATIONAL SETTLING
C      ROUTINE USING THE BAHCALL AND LOEB METHOD.
C FIRST DEFINE VARIABLES NEEDED FOR SETTLING -
C HQPR=VECTOR OF D LN P/DR.
C STOT=TOTAL STELLAR MASS(UNLOGGED).
      IF(LDIFY)THEN
         IF(HCOMP(1,1).LT.XMIN)THEN
            LDIFY=.FALSE.
            GOTO 170
         ENDIF
C MHP 6/90 CHANGE ADDED : THE TIMESTEP FOR SETTLING IS RESTRICTED TO
C   A FRACTION OF THE TIMESCALE FOR SETTLING AT THE OUTER BOUNDARY.
C   THE OUTER BOUNDARY IS EITHER THE SURFACE CONVECTION ZONE OR THE
C   FIRST POINT WHERE THE HELIUM ABUNDANCE RISES ABOVE A USER-SPECIFIED
C   MINIMUM VALUE (YMIN).
C
C   LOCATE OUTER BOUNDARY.
         IF(.NOT.LC(M))THEN
            WRITE(*,911)
            WRITE(ISHORT,911)
  911       FORMAT(1X,'NO SURFACE CZ - DIFFUSION NOT MEANINGFUL'/
     *             'STOPPED IN SUBROUTINE MIX')
            STOP
         ENDIF
C 7/92 MHP STATEMENT ADDED FOR EXIT IF OVERSHOOT CAUSES A FULLY CONVECTIVE
C CASE.  AVOIDS OCCASIONAL PRE-MS CRASH IN FIRST RADIATIVE MODEL.
         IF(MXZONE(NZONE,1).LE.2 .AND. MXZONE(NZONE,2).EQ.M)GOTO 170
         JMAX = MRZONE(NRZONE,2)
         DO 140 J = JMAX,1,-1
            IF(HCOMP(2,J).GT.YMIN)GOTO 150
  140    CONTINUE
C   Y<YMIN FOR THE WHOLE STAR IF THE CODE GETS HERE.
         GOTO 170
  150    CONTINUE
         STOT=EXP(CLN*HSTOT)
         DO 130 I = 1,M
            TEMP(I) = SDEL(2,I)
            SDEL(2,I) = DELM(I)
            HQPR(I)=-EXP(CLN*(HD(I)+CGL+HS(I)-2.0D0*HR(I)-HP(I)))
  130    CONTINUE
C ***BC 6/92 only check for timestep cutting if JMAX is large
C
         IF(JMAX.GE.2) THEN
C  FM IS THE MASS FRACTION ABOVE THE OUTER POINT.
            FM = (STOT-HS1(JMAX))/STOT
C  TSCALE IS THE TIMESCALE FOR SETTLING OF HELIUM AT THE OUTER
C  BOUNDARY (MICHAUD ET AL 1984, APJ V.282,P.206)
            TSCALE = 4.348D21*CSECYR*FM/EXP(CLN*1.5D0*HT(JMAX))
C  RESTRICT TIMESTEP TO THE MINIMUM OF THE MODEL TIMESTEP AND
C  A USER SPECIFIED FRACTION (DT_GS) OF THE SETTLING TIMESCALE.
            write(69,*) 'JMAX=',JMAX,' FM= ',fm,' TSCALE=',TSCALE
            DTMAX = DT_GS*TSCALE
            NSTEP = INT(DELTS/DTMAX)
            IF(MOD(DTMAX,DELTS).NE.0.0D0.OR.NSTEP.EQ.0)NSTEP=NSTEP+1
            write(69,*)'DTMAX=',dtmax,' DELTS=',delts,' NSTEP=',nstep
            DT = DELTS/DFLOAT(NSTEP)
         ELSE
            NSTEP=1
            DT = DELTS
         ENDIF
         DO J = 1,NRZONE
            DO I = MRZONE(J,1),MRZONE(J,2)
               LCZ(I) = .FALSE.
            END DO
         END DO
         DO J = 1,NZONE
            DO I = MXZONE(J,1),MXZONE(J,2)
               LCZ(I) = .TRUE.
            END DO
         END DO
         DO 160 NS = 1,NSTEP
C PERFORM GRAVITATIONAL SETTLING. IF LNEWDIF = TRUE, USE THE NEW ROUTINES
C IN MICRODIFF. ELSE, USE THE OLD ROUTINES IN GRSETT.
            IF(LNEWDIF)THEN
               CALL MICRODIFF(DT,HCOMP,HQPR,HR,HD,HS1,HT,LCZ,M,STOT)
            ELSE
               CALL GRSETT(DT,HCOMP,HQPR,HR,HD,HS1,HT,LCZ,M,STOT)
            ENDIF
  160    CONTINUE
         DO I = 1,M
            SDEL(2,I) = TEMP(I)
         END DO
  170    CONTINUE
      ENDIF
C END OF GRAVITATIONAL SETTLING
C
C RENORMALIZE COMPOSITION TO GUARD AGAINST ANOMALIES (I.E. SMALL NEGATIVE
C ABUNDANCES...).
      DO 180 I = 1,M
         DO 175 J = 1,NSPEC
            HCOMP(J,I) = MAX(HCOMP(J,I),0.0D0)
            HCOMP(J,I) = MIN(HCOMP(J,I),1.0D0)
  175    CONTINUE
         HCOMP(3,I) = MIN(HCOMP(3,I),1.0D0-HCOMP(1,I)-HCOMP(4,I))
         HCOMP(2,I) = 1.0D0 - HCOMP(1,I) - HCOMP(3,I) - HCOMP(4,I)
  180 CONTINUE
      RETURN
      END
