C $$$$$$
C       SUBROUTINE PHYSIC(FP,FT,HCOMP,HD,HG,HL,HP,HR,HS,HT,LC,LCZ,M,TEFFL)  ! KC 2025-05-31
      SUBROUTINE PHYSIC(FP,FT,HCOMP,HD,HG,HL,HP,HR,HS,HT,LC,M,TEFFL)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)

      COMMON/COMP/XENV,ZENV,ZENVM,AMUENV,FXENV(12),XNEW,ZNEW,STOTAL,
     *     SENV
      COMMON/COMP2/YENV,Y3ENV
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/FLAG/LEXCOM
      COMMON/OPTAB/OPTOL,ZSI,IDT,IDD(4)
      COMMON/SCRTCH/SESUM(JSON),SEG(7,JSON),SBETA(JSON),SETA(JSON),
     * LOCONS(JSON),SO(JSON),SDEL(3,JSON),SFXION(3,JSON),SVEL(JSON),SCP(JSON)
      COMMON/TEMP/CP(JSON),HAMU(JSON),SQDT(JSON),THDIF(JSON),
     *     VISC(JSON)
      COMMON/DWMAX/QWRMAX(JSON),QWRMX0(JSON)
      COMMON/MHD/LMHD,IOMHD1,IOMHD2,IOMHD3,IOMHD4,IOMHD5,IOMHD6,
     1     IOMHD7, IOMHD8
C DBG 7/92 COMMON BLOCK ADDED TO COMPUTE DEBYE-HUCKEL CORRECTION.
      COMMON/DEBHU/CDH,ETADH0,ETADH1,ZDH(18),XXDH,
     1     YYDH,ZZDH,DHNUE(18),LDH
      COMMON/DPMIX/DPENV,ALPHAC,ALPHAE,ALPHAM,BETAC,IOV1,IOV2,
     *      IOVIM, LOVSTC, LOVSTE, LOVSTM, LSEMIC, LADOV, LOVMAX
      DIMENSION HCOMP(15,JSON),HD(JSON),HL(JSON),HG(JSON),
     *     HP(JSON),HR(JSON),HS(JSON),HT(JSON),LC(JSON),
C      *     FXION(3),FP(JSON),FT(JSON),ATOMWT(4),LCZ(JSON),  ! KC 2025-05-31
     *     FXION(3),FP(JSON),FT(JSON),ATOMWT(4),
     *     HF(4),HOLD(4)
      DATA ATOMWT/1.007825D0,4.002603D0,12.0D0,3.01603D0/
      SAVE

C  FIND ACTUAL AND ADIABATIC TEMPERATURE GRADIENTS,OPACITY,AND
C  MEAN MOLECULAR WEIGHT FOR ALL RADIATIVE SHELLS.
      LDERIV = .FALSE.
      LOCOND = .FALSE.
      LATMO = .FALSE.
      IDT = 15
      DO 25 I = 1,4
         IDD(I) = 5
 25   CONTINUE
      DO 30 IM = 1,M
         SL = HS(IM)
         TL = HT(IM)
         PL = HP(IM)
         RL = HR(IM)
         B = HL(IM)
         X = HCOMP(1,IM)
         Z = HCOMP(3,IM)
         DL = HD(IM)
         FPL = FP(IM)
         FTL = FT(IM)

         IF(LMHD) THEN
            CALL MEQOS(TL,T,PL,P,DL,D,X,Z,BETA,BETAI,BETA14,FXION,RMU,
     *           AMU,EMU,ETA,QDT,QDP,QCP,DELA,QDTT,QDTP,QAT,QAP,QCPT,QCPP)
C      *           QCPP,LDERIV,LATMO,KSAHA)  ! KC 2025-05-31
         ELSE
            IF (LDH) THEN
               XXDH = HCOMP(1,IM)
               YYDH = HCOMP(2,IM)+HCOMP(4,IM)
               ZZDH = HCOMP(3,IM)
               ZDH(1) = HCOMP(5,IM)+HCOMP(6,IM)
               ZDH(2) = HCOMP(7,IM)+HCOMP(8,IM)
               ZDH(3) = HCOMP(9,IM)+HCOMP(10,IM)+HCOMP(11,IM)
            END IF
            CALL EQSTAT(TL,T,PL,P,DL,D,X,Z,BETA,BETAI,BETA14,FXION,RMU,
     *           AMU,EMU,ETA,QDT,QDP,QCP,DELA,QDTT,QDTP,QAT,QAP,QCPT,
     *           QCPP,LDERIV,LATMO,KSAHA)
         ENDIF
         CALL GETOPAC (DL,TL,X,Z,O,OL,QOD,QOT,FXION)
         IOVIM=IM
         CALL TPGRAD(TL,T,PL,P,D,RL,SL,B,O,QDT,QDP,QOT,QOD,QCP,DEL,
     *        DELR,DELA,QDTT,QDTP,QAT,QAP,QACT,QACP,QACR,QCPT,QCPP,
     *        VEL,LDERIV,LCONV,FPL,FTL,TEFFL)
         LC(IM) = LCONV
         SDEL(1,IM) = DELR
         SDEL(2,IM) = DEL
         SDEL(3,IM) = DELA
C  FIND NEW RUN OF MEAN MOLECULAR WEIGHT ASSUMING FULLY IONIZED GAS.
C  AMUENV IS(1/MEAN MOLECULAR WEIGHT PER ION OF THE SURFACE MIXTURE.)
         DFX1 = HCOMP(1,IM) - XENV
         DFX2 = HCOMP(2,IM) - YENV
         DFX3 = HCOMP(3,IM) - ZENV
         DFX4 = HCOMP(4,IM) - Y3ENV
         TEMP = AMUENV + DFX1/ATOMWT(1) + DFX2/ATOMWT(2) +
     *        DFX3/ATOMWT(3) + DFX4/ATOMWT(4)
         AMU2 = 1.0D0/TEMP
         TEMP = HCOMP(1,IM)/ATOMWT(1) + 2.0D0*(HCOMP(4,IM)/ATOMWT(4)
     *        + HCOMP(2,IM)/ATOMWT(2)) + 0.5D0*HCOMP(3,IM)
         EMU2 = 1.0D0/TEMP
         HAMU(IM) = AMU2*EMU2/(AMU2+EMU2)
         SO(IM) = O
         CP(IM) = QCP
         SQDT(IM) = QDT
C JVS 10/13 Always want SVEL
         SVEL(IM) = VEL
C         IF(LC(IM))THEN
C            SVEL(IM) = VEL
C         ELSE
C            SVEL(IM) = 0.0D0
C         ENDIF
 30   CONTINUE
C  FIND THE THERMOMETRIC DIFFUSIVITY AND KINEMATIC VISCOSITY.
C       CALL VISCOS(HCOMP,HD,HT,LC,M)  ! KC 2025-05-31
      CALL VISCOS(HCOMP,HD,HT,M)
C  FIND THE RUN OF MAXIMUM ALLOWABLE D OMEGA/D LNR AND THE RUN OF ACTUAL
C  D OMEGA/D LN R FOR ALL RADIATIVE ZONES.
C  IF THE GRADIENT OF OMEGA EXCEEDS THE CRITICAL VALUE FOR THE SHEAR
C  INSTABILITY, A SHORT TIMESCALE INSTABILITY OCCURS.  MIX ALL ADJACENT
C  UNSTABLE ZONES TO A MARGINALLY STABLE STATE.
      DO 100 IM = 2,M
C  SKIP CONVECTIVE REGIONS
         IF(LC(IM).AND.LC(IM-1))THEN
            QWRMAX(IM) = 0.0D0
            GOTO 100
         ENDIF
C  NOW CHECK FOR SHEAR INSTABILITY -REF.ENDAL&SOFIA APJ 220:279(1978)
C  THERMODYNAMIC QUANTITIES ARE CALCULATED AT THE SHELL MIDPOINT BY
C  4-POINT LAGRANGIAN INTERPOLATION.
         IF(IM.LE.2) THEN
            K = 1
         ELSE IF(IM.EQ.M) THEN
            K = M - 3
         ELSE
            K = IM - 2
         ENDIF
         DO 90 I = 1, 4
            HOLD(I) = HS(I+K-1)
 90      CONTINUE
C  USE 4-POINT LAGRANGIAN INTERPOLATION TO FIND PHYSICAL VARIABLES
C  AT THE INTERFACE BEING TESTED.
         HSMID = 0.5D0*(HS(IM) + HS(IM-1))
         CALL INTRP2(HOLD,HF,HSMID)
         PMID=HP(K)*HF(1) + HP(K+1)*HF(2) + HP(K+2)*HF(3) +HP(K+3)*HF(4)
         DMID=HD(K)*HF(1) + HD(K+1)*HF(2) + HD(K+2)*HF(3) +HD(K+3)*HF(4)
         DELMID=SDEL(2,K)*HF(1) + SDEL(2,K+1)*HF(2) + SDEL(2,K+2)*HF(3)
     *        + SDEL(2,K+3)*HF(4)
         DELAMD=SDEL(3,K)*HF(1) + SDEL(3,K+1)*HF(2) + SDEL(3,K+2)*HF(3)
     *        + SDEL(3,K+3)*HF(4)
         GMID=HG(K)*HF(1) + HG(K+1)*HF(2) + HG(K+2)*HF(3) +HG(K+3)*HF(4)
         TEMP = DEXP(CLN*(DMID - PMID))*(DELAMD - DELMID)*GMID**2
         IF(TEMP.GT.0.0D0) THEN
            QWRMAX(IM) = 2.0D0*DSQRT(TEMP)
         ELSE
            QWRMAX(IM) = 0.0D0
         ENDIF
 100  CONTINUE

      RETURN
      END
