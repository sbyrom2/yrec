

C $$$$$$
      SUBROUTINE GETROT(HD,HJM,HR,HS,HS2,LCZ,M,ETA2,HI,OMEGA,QIW,R0)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      DIMENSION HD(JSON),HJM(JSON),HR(JSON),HS(JSON),HS2(JSON),
     *          LCZ(JSON),ETA2(JSON),HI(JSON),OMEGA(JSON),QIW(JSON),
     *          R0(JSON)
      SAVE
C  GETW TAKES THE ANGULAR MOMENTUM DISTRIBUTION AND FINDS THE
C  ROTATION CURVE THAT CORRESPONDS TO IT.  CONVECTIVE REGIONS HAVE
C  SOLID BODY ROTATION ENFORCED ON THEM.
      LCONVZ = .FALSE.
      DO 20 I = 1,M
         IF(.NOT.LCONVZ) THEN
            IF(.NOT.LCZ(I)) THEN
C  RADIATIVE SHELL
               JSTART = I
               JEND = I
               CALL SOLID(HD,HJM,HR,HS,HS2,JSTART,JEND,ETA2,HI,OMEGA,
     *          QIW,R0,M)
            ELSE
C  CONVECTION ZONE START - SET LCONVZ TO T
               LCONVZ = .TRUE.
               JSTART = I
            ENDIF
         ELSE IF(.NOT.LCZ(I)) THEN
C  THE PREVIOUS SHELL WAS CONVECTIVE.  IF THIS ONE IS RADIATIVE, ENFORCE
C  THE APPROPRIATE ROTATION LAW IN THE ENTIRE CONVECTION ZONE PRECEDING IT.
            JEND = I - 1
C JNT 09/25 FOR 05/15 CALL WCZIMP INSTEAD OF WCZ
            CALL WCZIMP(HD,HJM,HR,HS,HS2,JSTART,JEND,ETA2,HI,OMEGA,
     *               QIW,R0,M)
C  NOW FIND OMEGA IN THE CURRENT SHELL
            JSTART = I
            JEND = I
            CALL SOLID(HD,HJM,HR,HS,HS2,JSTART,JEND,ETA2,HI,OMEGA,QIW,
     *       R0,M)
            LCONVZ = .FALSE.
         ELSE IF(I.EQ.M) THEN
C  SPECIAL CASE OF LAST SHELL CONVECTIVE - ENFORCE SOLID-BODY ROTATION
C  IN CZ AS ABOVE.
            JEND = M
C JNT 09/25 FOR 05/15 CALL WCZIMP INSTEAD OF WCZ
            CALL WCZIMP(HD,HJM,HR,HS,HS2,JSTART,JEND,ETA2,HI,OMEGA,
     *               QIW,R0,M)
         ENDIF
   20 CONTINUE
      RETURN
      END
