C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C SCONVEC
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE SCONVEC(DELTS,HCOMP,HD,HL,HP,HR,HS,HT,M,MXZONE,NZONE,TEFFL)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
C DBGLAOL
      REAL*8 OLAOL(12,104,52),OXA(12),OT(52),ORHO(104),TOLLAOL
C MHP 8/25 Removed unused variables
C      CHARACTER*256 FLAOL, FPUREZ
      COMMON/FLAG/LEXCOM
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      COMMON/CONST/CLSUN,CLSUNL,CLNSUN,CMSUN,CMSUNL,CRSUN,CRSUNL,CMBOL
      COMMON/CONST1/CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
C MHP 8/25 Removed character file names from common block
C DBGLAOL
      COMMON/NWLAOL/OLAOL, OXA, OT, ORHO, TOLLAOL,
     *  IOLAOL, NUMOFXYZ, NUMRHO, NUMT, LLAOL, LPUREZ, IOPUREZ
C DBG 7/92 COMMON BLOCK ADDED TO COMPUTE DEBYE-HUCKEL CORRECTION.
      COMMON/DEBHU/CDH,ETADH0,ETADH1,ZDH(18),XXDH,
     1             YYDH,ZZDH,DHNUE(18),LDH
      DIMENSION HCOMP(15,JSON),HD(JSON),HP(JSON),HR(JSON),HS(JSON),
     *          HT(JSON),MXZONE(12,2),FXION(3),HL(JSON)
      SAVE

C SEMI-CONVECTION IS TESTED FOR ALL CONVECTIVE REGIONS IN THIS SUBROUTINE.
C THE REFERENCE FOR THIS SECTION IS :
C CASTELLANI ET AL.(1971)ASTROPHYSICS AND SPACE SCIENCE, VOL. 10, PP.340-349.
C
C AT THE EDGE OF ALL CONVECTIVE REGIONS, THE RADIATIVE TEMPERATURE GRADIENT
C IS COMPUTED WITH BOTH THE LOCAL ABUNDANCES AND THOSE IN THE NEIGHBORING
C CONVECTION ZONE.  IF DEL(RAD)>DEL(AD) WHEN THE ABUNDANCES ARE PERTURBED,
C THEN THE REGION IS CONSIDERED UNSTABLE TO THE GROWTH OF A SEMI-CONVECTIVE
C REGION AND THE CODE COMPUTES ITS EXTENT.
C
C INPUT VARIABLES :
C
C DELTS : MODEL TIMESTEP (SECONDS)
C HD,HL,HP,HR,HS,HT : MODEL RUN OF DENSITY, LUMINOSITY, PRESSURE, RADIUS,
C   MASS, AND TEMPERATURE.
C HCOMP : MODEL RUN OF MASS FRACTIONS OF DIFFERENT CHEMICAL SPECIES;
C   HCOMP(1,...) IS HYDROGEN AND HCOMP(3,...) IS THE METALS.
C MXZONE, NZONE : THE LOCATIONS OF THE EDGES OF CONVECTIVE REGIONS,
C   STORED IN PAIRS, AND THE NUMBER OF DISTINCT CONVECTION ZONES.
C   E.G. MXZONE(3,1) IS THE ID OF THE BOTTOM SHELL IN THE 3RD CZ FROM
C   THE CENTER AND MXZONE(3,2) IS THE ID OF THE TOP SHELL IN THE SAME CZ.
C
C OUTPUT VARIABLES :
C
C MXZONE, NZONE CAN BE ALTERED BY THE SUBROUTINE.
C
C RUN THROUGH ALL THE CONVECTION ZONES.
      DO 210 I = 1,NZONE
C DETERMINE IF THIS REGION IS A CORE CONVECTION ZONE, SURFACE CZ,
C OR INTERMEDIATE CZ.
C LFLAGU IS T IF SEMICONVECTION ABOVE THE CZ NEEDS TO BE COMPUTED;
C LFLAGD IS T IF SEMICONVECTION BELOW THE CZ NEEDS TO BE COMPUTED.
         IF(MXZONE(I,1).EQ.1)THEN
C CONVECTIVE CORE
C CHECK FOR A FULLY CONVECTIVE STAR; SKIP THIS SUBROUTINE IF THERE IS ONE.
            IF (MXZONE(I,2).EQ.M) RETURN
            LFLAGU = .TRUE.
            LFLAGD = .FALSE.
         ELSE IF(MXZONE(I,2).EQ.M)THEN
C CONVECTIVE ENVELOPE
            LFLAGU = .FALSE.
            LFLAGD = .TRUE.
         ELSE
C INTERMEDIATE CONVECTION ZONE (NOT INCLUDING CENTRAL OR SURFACE POINT).
            LFLAGU = .TRUE.
            LFLAGD = .TRUE.
         ENDIF
C CHECK SEMICONVECTION BELOW (K=1) AND ABOVE (K=2) THE CZ.
         DO 200 K = 1,2
C SKIP SEMI-CONVECTION BELOW A CENTRAL CZ AND ABOVE A SURFACE ONE.
            IF(K.EQ.1.AND..NOT.LFLAGD) GOTO 200
            IF(K.EQ.2.AND..NOT.LFLAGU) GOTO 200
C JMC AND JMR ARE THE LOCATIONS OF THE EDGE OF THE CONVECTIVE ZONE
C AND THE FIRST RADIATIVE POINT OUTSIDE IT RESPECTIVELY.
            JMC = MXZONE(I,K)
            IF(K.EQ.1) JMR = JMC - 1
            IF(K.EQ.2) JMR = JMC + 1
C SET UP FLAGS FOR CALLS TO THE BASIC PHYSICS ROUTINES.
            LDERIV = .FALSE.
            LOCOND = .TRUE.
            LATMO = .TRUE.
            KSAHA = 0
C USE THE STRUCTURE VARIABLES FOR THE FIRST POINT OUTSIDE THE CZ
C AND THE ABUNDANCES OF THE CZ TO DETERMINE THE MODIFIED MU AND
C RADIATIVE AND ADIABATIC TEMPERATURE GRADIENTS.
            B = HL(JMR)
            SL = HS(JMR)
            PL = HP(JMR)
            TL = HT(JMR)
            DL = HD(JMR)
C MHP 10/02 added definition of RL
            RL = HR(JMR)
            X = HCOMP(1,JMC)
            Z = HCOMP(3,JMC)
C*** ADD ROTATION VECTORS ***
            FPL = 1.0D0
            FTL = 1.0D0
            IU=JMC
            IF (LDH) THEN
               XXDH = HCOMP(1,JMC)
               YYDH = HCOMP(2,JMC)+HCOMP(4,JMC)
               ZZDH = HCOMP(3,JMC)
               ZDH(1) = HCOMP(5,JMC)+HCOMP(6,JMC)
               ZDH(2) = HCOMP(7,JMC)+HCOMP(8,JMC)
               ZDH(3) = HCOMP(9,JMC)+HCOMP(10,JMC)+HCOMP(11,JMC)
            END IF
            CALL EQSTAT(TL,T,PL,P,DL,D,X,Z,BETA,BETA1,BETA14,FXION,
     *         RMU,AMU,EMU,ETA,QDT,QDP,QCP,DELA,QDTT,QDTP,QAT,QAP,
     *         QCPT,QCPP,LDERIV,LATMO,KSAHA)
C DBG 12/95 GET OPACITY
            CALL GETOPAC(DL, TL, X, Z, O, OL, QOD, QOT, FXION)
            CALL TPGRAD(TL,T,PL,P,D,RL,SL,B,O,QDT,QDP,QOT,QOD,QCP,
     *           DEL,DELR,DELA,QDTT,QDTP,QAT,QAP,QACT,QACP,QACR,QCPT,
     *           QCPP,VEL,LDERIV,LCONV,FPL,FTL,TEFFL)
C SKIP IF ZONE IS STABLE WITH THE CORE COMPOSITION.
            IF(DELR.LT.DELA) GOTO 200
C STORE MEAN MOLECULAR WEIGHT, ADJUSTED RADIATIVE TEMPERATURE GRADIENT,
C AND THE QUANTITY (DELR - DELA)/DELR.
            XINTMU = AMU + EMU
            DELR1 = DELR
            RMAX = 1.0D0 - DELA/DELR
C REPEAT CALL WITH THE LOCAL COMPOSITION.
            X = HCOMP(1,JMR)
            Z = HCOMP(3,JMR)
            IU=JMR
            IF (LDH) THEN
               XXDH = HCOMP(1,JMR)
               YYDH = HCOMP(2,JMR)+HCOMP(4,JMR)
               ZZDH = HCOMP(3,JMR)
               ZDH(1) = HCOMP(5,JMR)+HCOMP(6,JMR)
               ZDH(2) = HCOMP(7,JMR)+HCOMP(8,JMR)
               ZDH(3) = HCOMP(9,JMR)+HCOMP(10,JMR)+HCOMP(11,JMR)
            END IF
            CALL EQSTAT(TL,T,PL,P,DL,D,X,Z,BETA,BETA1,BETA14,FXION,
     *         RMU,AMU,EMU,ETA,QDT,QDP,QCP,DELA,QDTT,QDTP,QAT,QAP,
     *         QCPT,QCPP,LDERIV,LATMO,KSAHA)
C DBG 12/95 GET OPACITY
            CALL GETOPAC(DL, TL, X, Z, O, OL, QOD, QOT, FXION)
            CALL TPGRAD(TL,T,PL,P,D,RL,SL,B,O,QDT,QDP,QOT,QOD,QCP,
     *           DEL,DELR,DELA,QDTT,QDTP,QAT,QAP,QACT,QACP,QACR,QCPT,
     *           QCPP,VEL,LDERIV,LCONV,FPL,FTL,TEFFL)
            HD(JMR) = DL
C FDEL IS THE RATIO OF THE GRADIENTS WITH THE OLD COMP AND NEW ONE.
            FDEL = DELR1/DELR
            DELR0 = DELR
C RMAX IS THE MAXIMUM RADIAL DISTANCE THAT OVERSHOOT MAY PENETRATE, AND
C IS THE TIMESTEP(DELTS) MULTIPLIED BY THE VELOCITY OF PROPAGATION OF
C THE INSTABILITY (VP IN EQUATION 5PRIME, P. 347).
            RMAX = RMAX*(HL(JMC)*CLSUN/(10.0D0*C4PI*DEXP(CLN*HP(JMC))))
     *             *(DELTS/DEXP(CLN*(HR(JMC)+HR(JMC))))
C INITIALIZE SUM.
            RSUM = 0.0D0
C LMAX IS SET TO TRUE IF THE OVERSHOOT REACHES ITS MAXIMUM EXTENT.
            LMAX = .FALSE.
            IF(K.EQ.1)THEN
               RVAL1 = EXP(CLN*HR(JMR+1))
               JBEG = JMR-1
               JEND = 1
               JSTEP = -1
            ELSE IF(K.EQ.2)THEN
               RVAL1 = EXP(CLN*HR(JMR-1))
               JBEG = JMR + 1
               JEND = M
               JSTEP = 1
            ENDIF
            DO 32 II = JBEG,JEND,JSTEP
C TEST ON MAXIMUM OVERSHOOTING LIMIT IN RADIUS
               IF(K.EQ.1) RVAL2 = EXP(CLN*HR(II+1))
               IF(K.EQ.2) RVAL2 = EXP(CLN*HR(II-1))
               RDIFF = RVAL2-RVAL1
               RSUM = RSUM + (1.0D0 - XINTMU/(AMU+EMU))*RDIFF
               RVAL1 = RVAL2
               IF(RSUM.GT.RMAX) GO TO 33
C TEST ON RESCALED RAD. GRADIENT FOR CONVECTION
               B = HL(II)
               SL = HS(II)
               PL = HP(II)
               TL = HT(II)
               DL = HD(II)
               X = HCOMP(1,II)
               Z = HCOMP(3,II)
               IU = II
               IF (LDH) THEN
                  XXDH = HCOMP(1,II)
                  YYDH = HCOMP(2,II)+HCOMP(4,II)
                  ZZDH = HCOMP(3,II)
                  ZDH(1) = HCOMP(5,II)+HCOMP(6,II)
                  ZDH(2) = HCOMP(7,II)+HCOMP(8,II)
                  ZDH(3) = HCOMP(9,II)+HCOMP(10,II)+HCOMP(11,II)
               END IF
               CALL EQSTAT(TL,T,PL,P,DL,D,X,Z,BETA,BETA1,BETA14,FXION,
     *         RMU,AMU,EMU,ETA,QDT,QDP,QCP,DELA,QDTT,QDTP,QAT,QAP,
     *         QCPT,QCPP,LDERIV,LATMO,KSAHA)
C DBG 12/95 GET OPACITY
               CALL GETOPAC(DL, TL, X, Z, O, OL, QOD, QOT, FXION)
               CALL TPGRAD(TL,T,PL,P,D,RL,SL,B,O,QDT,QDP,QOT,QOD,
     *              QCP,DEL,DELR,DELA,QDTT,QDTP,QAT,QAP,QACT,QACP,
     *              QACR,QCPT,QCPP,VEL,LDERIV,LCONV,FPL,FTL,TEFFL)
               HD(II) = DL
C EXIT IF ZONE IS RADIATIVELY STABLE.
               IF(FDEL*DELR.LT.DELA) GO TO 34
   32       CONTINUE
            IF(K.EQ.1) II = 0
            IF(K.EQ.2) II = M + 1
   33       LMAX = .TRUE.
   34       CONTINUE
C ASSIGN THE NEW EDGE LOCATION TO MXZONE.
            IF(K.EQ.1)THEN
               MXZONE(I,K) = II + 1
               JMC2 = II + 1
            ELSE IF(K.EQ.2)THEN
               MXZONE(I,K) = II - 1
               JMC2 = II - 1
            ENDIF
            WRITE(ISHORT,601) JMC,JMC2,LMAX,DELR1,DELR0
  601       FORMAT(1X,'CZ OLD EDGE ',I3,' EXTENDED TO-',
     *             I3,' LIMIT=',L1,' RAD.GRADS-IN/OUT',2F8.4)
  200    CONTINUE
  210 CONTINUE
C  CHECK FOR MERGERS OF NEARBY CONVECTION ZONES CAUSED BY SEMI-CONVECTION.
      IF(NZONE.LE.1) RETURN
      J = 1
   85 CONTINUE
C  CHECK IF 'TOP' OF ONE REGION IS ABOVE 'BOTTOM' OF THE NEXT ONE.
      IF(MXZONE(J,2).GT.MXZONE(J+1,1))THEN
C  IF THIS OCCURS, TWO CONVECTION ZONES HAVE MERGED.
         WRITE(ISHORT,93)((MXZONE(K,K1),K1=1,2),K=J,J+1),MXZONE(J,1),
     *              MXZONE(J+1,2)
   93    FORMAT(2X,'MIXED ZONES MERGED DUE TO SEMICONVECTION'
     *          /2X,'OLD',2('[',I3,'-',I3,']'),
     *          ' NEW','[',I3,'-',I3,']')
         MXZONE(J+1,1) = MXZONE(J,1)
         DO 90 K = J,NZONE-1
            DO 95 J2 = 1,2
               MXZONE(K,J2) = MXZONE(K+1,J2)
   95       CONTINUE
   90    CONTINUE
         NZONE = NZONE - 1
         IF(J.LE.NZONE-1)THEN
            GOTO 85
         ELSE
            RETURN
         ENDIF
      ENDIF
      J = J + 1
      IF (J.LE.NZONE-1)GOTO 85

      RETURN
      END
