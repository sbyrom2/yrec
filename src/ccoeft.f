C $$$$$$
      SUBROUTINE CCOEFT(COD,DR,DT,ECOMP,EM,NTOT)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CCOUT/LSTORE,LSTATM,LSTENV,LSTMOD,LSTPHYS,LSTROT,LSCRIB,LSTCH,LPHHD
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      COMMON/TRIDI/A(JSON),B(JSON),C(JSON),D(JSON),U(JSON),GAMA(JSON)
      DIMENSION COD(JSON),ECOMP(JSON),EM(JSON)
      SAVE
C  SET UP COEFFICIENTS OF DIFFUSION DIFFERENCE EQUATIONS FOR COMPOSITION
C  TRANSPORT DUE TO ROTATIONALLY INDUCED MIXING.
C
C  INPUT VARIABLES:
C  COD : RUN OF DIFFUSION COEFFICIENTS (COMPUTED IN SR COEFFT).
C  DR : GRID SPACING IN RADIUS(CM).
C  DT : DIFFUSION TIMESTEP (SEC).
C  ECOMP : STARTING ARRAY OF COMPOSITION MASS FRACTION AT THE EQUALLY
C          SPACED GRID.
C  EM : RUN OF MASSES OF THE EQUALLY SPACED GRID POINTS (GM).
C  NTOT : NUMBER OF POINTS IN THE EQUALLY SPACED GRID USED TO SOLVE
C         THE DIFFUSION EQUATION.
C  OUTPUT VARIABLES :
C  THE ABUNDANCE OF SHELL I AT TIME N+1 (COMP(I,N+1)) IS A FUNCTION OF
C  COMP(I-1),COMP(I),AND COMP(I+1) AS DISCUSSED BELOW.  THIS CAN BE EXPRESSED
C  AS A TRIDIAGONAL MATRIX.  TERMS WHICH DEPEND ON COMP(I-1) ARE STORED IN
C  ARRAY A; THOSE WHICH DEPEND ON COMP(I) ARE STORED IN ARRAY B; THOSE
C  WHICH DEPEND ON COMP(I+1) ARE STORED IN ARRAY C; AND THE STARTING RUN
C  OF COMPOSITION IS STORED IN ARRAY D.
C
C  THE DIFFUSION EQUATION WE ARE SOLVING IS
C  dX/dT = 1/(4pi*RHO*R**2)d/dR(D*dX/dR)
C  WHERE D IS OUR DIFFUSION COEFFICIENT,X IS THE MASS FRACTION OF THE
C  SPECIES IN QUESTION,T IS TIME,R IS RADIUS, AND RHO IS DENSITY.
C  USING M = 4pi*RHO*R**2*DR THIS IS DIFFERENCED FOR SHELL I AS
C  X(I,N+1)-X(I,N)=(DT/DR)*(1/M)*(D(I+1/2)*(X(I+1,N+1)-X(I,N+1)) -
C  D(I-1/2)*(X(I,N+1)-X(I-1,N+1)))
C  WHERE M IS THE MASS OF SHELL , INDEX N+1 REFERS TO VALUES AT THE
C  END OF THE TIMESTEP, INDEX N REFERS TO VALUES AT THE BEGINNING OF
C  THE TIMESTEP.
C  THIS SYSTEM IS SUPPLEMENTED WITH APPROPRIATE BOUNDARY CONDITIONS.
C
C  STORE THE RUN OF COMPOSITION AT THE BEGINNING OF THE TIMESTEP IN ARRAY D
      DO 10 I = 1,NTOT
         D(I) = ECOMP(I)
   10 CONTINUE
C  FIRST SHELL BOUNDARY CONDITIONS: NO FLOW BELOW THE BOUNDARY
C  I.E. THE ANGULAR MOMENTUM TRANSPORT AT THE FIRST SHELL DOES NOT
C  DEPEND ON THE SHELLS BELOW IT.(SAME USED FOR LAST SHELL B.C.)
      FACT0 = DT/DR
      FACT = FACT0/EM(1)
      A(1) = 0.0D0
      B(1) = 1.0D0 + FACT*COD(2)
      C(1) = -FACT*COD(2)
      DO 20 I = 2,NTOT-1
         FACT = FACT0/EM(I)
         A(I) = -FACT*COD(I)
         B(I) = 1.0D0 + FACT*(COD(I)+COD(I+1))
         C(I) = -FACT*COD(I+1)
   20 CONTINUE
C  LAST SHELL BOUNDARY CONDITIONS.
      FACT = FACT0/EM(NTOT)
      A(NTOT) = -FACT*COD(NTOT)
      B(NTOT) = 1.0D0 + FACT*COD(NTOT)
      C(NTOT) = 0.0D0

      RETURN
      END
