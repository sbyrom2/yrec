
C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C KURUCZ
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     THIS IS INTERPOLATION FACILITIES FOR KURUCZ'S OPACITY TABLES
C     USING CUBIC SPLINE INTERPLOATION SCHEME.

      SUBROUTINE KURUCZ(DL,TL,OF,OLF,QODF,QOTF,*)

C     TWO DIMENSIONAL INTERPOLATION FOR OPACITY
C     M1, M2, AND M3 ARE NEAREST GRID POINT OF ABUNDANCE, TEMPERATURE,
C     AND DENSITY WE GOT AT 'GETD'.
C     O IS OPACITY.
C     OL IS DLOG(O)
C     QODF IS THE PARTIAL DERIVATIVE OF O WRT D.
C     QOTF IS THE PARTIAL DERIVATIVE OF O WRT T.
C     THE VARIABLES WHICH HAVE 'TEM'-HEAD OR 'TEM'-TAIL ARE TEMPORARILLY
C     REQUIRED FOR DERIVATIVE VALUE ESTIMATION.  THESE ARE NOT THE VALUES
C     WE WANT.
C     THE VARIABLES WHICH HAVE 'I'-TAIL ARE OBTAINED FROM INTERPOLATION
C     ROUTINE.  WE WILL NOT USE THESE EVALUATIONS.
C     THE VARIABLES WHICH HAVE 'F'-TAIL ARE WHAT WILL BE RETURNED.
C     THIS VALUES IS ESTIMATED BY NUMERICAL DIFFERENTIATION.

      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4 (L)
      PARAMETER (NUMT=60)
      PARAMETER (NUMD=50)
      PARAMETER (NUMX=1)
      PARAMETER (NUMXT=NUMT*NUMX)
      PARAMETER (NUM4D=4*NUMD)
      REAL*8 XT(NUMT),YTO(NUMT)
      REAL*8 AQOD(NUMT)
      INTEGER JT,IT,ITS,ITF
      LOGICAL*4 LMORE
      COMMON /GKRZ/GRIDT(NUMT)
      COMMON /KRZ/OPACITY(NUMXT,NUMD),RHO(NUMXT,NUMD),NUMTM
      COMMON /KIPM/M1,M2,M3
      COMMON /INTPL2/ CFORD(NUMXT,NUM4D),NDS(NUMXT),NDD(NUMXT)
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      DATA  M1,M2,M3/1,1,1/
      SAVE

      CALL FINDEX(GRIDT,NUMTM,TL,M2)

      T=TL
      D=DL
      IF (.NOT.(DL.LE.-3.0D0 .AND. TL.LE.4.1D0)) RETURN 1
      LMORE=.TRUE.
C     FOR SIX GRID POINTS OF TEMPERATURE
      ITS=M2-2
      IF(ITS.LE.0)ITS=1
      ITF=M2+3
      IF(ITF.GT.NUMTM)ITF=NUMTM
 333  CONTINUE
      JT=0
      DO 300 IT=ITS,ITF
         INDEX1=IT
         NDSS=NDS(INDEX1)
         NDF=NDSS+NDD(INDEX1)-1
C FIND THE 'M3'
         IF(M3.LT.NDSS.OR.M3.GT.NDF)M3=NDF
         KIP=M3
         IF(D.LT.RHO(IT,KIP))THEN
            DO 211 IYC=KIP-1,NDSS,-1
               IF(RHO(IT,IYC).LE.D)THEN
                  KIP=IYC
                  GOTO 213
               ENDIF
 211        CONTINUE
            GO TO 300
         ELSE
            DO 212 IYC=KIP,NDF-1
               IF(RHO(IT,IYC+1).GT.D)THEN
                  KIP=IYC
                  GOTO 213
               ENDIF
 212        CONTINUE
            IF(RHO(IT,NDF).GE.D)THEN
               KIP=NDF
               GOTO 213
            ENDIF
            GO TO 300
         ENDIF
 213     M3=KIP
         KNOT=M3-NDSS+1
         INDEX2=4*(KNOT-1)
C NOW, (KNOT,KNOT+1) IS SUB-RANGE OF RHO WHICH CONTAINS D.
         DX=D-RHO(IT,M3)
         C1=CFORD(INDEX1,INDEX2+1)
         C2=CFORD(INDEX1,INDEX2+2)
         C3=CFORD(INDEX1,INDEX2+3)
         C4=CFORD(INDEX1,INDEX2+4)
C INTERPOLATION FOR OPACITY(OL) IN THE ENTRY D AND THE EACH T-GRID
C ESTIMATES THE PARTIAL DERIVATIVE OF OL WRT D
C EVALUATES THE INTERPOLATION VALUE IN THE SUB-RANGE WE DETERMINED.
         OL0=((C4*DX+C3)*DX+C2)*DX+C1
         QODI=(3.0D0*C4*DX+2.0D0*C3)*DX+C2
         JT=JT+1
         XT(JT)=GRIDT(IT)
         YTO(JT)=OL0
         AQOD(JT)=QODI
 300  CONTINUE
      IF(JT.LE.3)THEN
         IF(LMORE)THEN
            ITS=1
            ITF=NUMTM
            LMORE=.FALSE.
            GO TO 333
         ENDIF
         WRITE(ISHORT,*) 'ERROR KURUCZ OP: NO TABLE VALUE ', D,T
         STOP
      ENDIF
      IF(XT(1).GT.T.OR.XT(JT).LT.T) RETURN 1
C INTERPOLATION FOR THE OPACITY IN THE ENTRY T AND D.
C GET THE PARTIAL DERIVATIVE OF OL WRT T.
      CALL INTPOL(XT,YTO,JT,T,OL00,QOTF)
      OLF=OL00
      OF=10.0D0**OLF
C QOTF = D LN(O)/D LN(T)
C FIND THE PARTIAL DERIVATIVE VALUE OF OL WRT D IN THE GIVEN T AND D
      CALL INTPOL(XT,AQOD,JT,T,QODF,QODTI)
C QODF = D LN(O)/D LN(D)
      RETURN
      END
